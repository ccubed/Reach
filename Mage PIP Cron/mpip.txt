&CMD.MPIP Mage PIP=$+mpip:@pemit %#=[header(Mage Payment in Power)]%R[u(LIT.KEY)]%R[u(F.DISP.RESULTS,[if(isstaff(%#),1,%#)])]%R[footer()]
&F.DISP.RESULTS Mage PIP=[switch(%0,1,[iter(lattr(me/PIP_*),[u(F.PARSE,%i0)], ,%R)],[iter([u(F.GET.PLAYERS.PIPS,%0)],[u(F.PARSE,%i0)], ,%R)])]
&F.PARSE #8949=[setq(A,[xget(me,%0)])][setq(B,[ljust(extract(%0,2,1,_),5)][ljust([name([extract(%qA,1,1,:)])],27)][rjust([extract(%qA,2,1,:)],27)][rjust(extract(%qA,3,1,:),10)][rjust(extract(%qA,4,1,:),10)])][if(extract(%qA,5,1,:),[setq(B,[ansi(hr,%qB)])])]%qB
&F.GET.PLAYERS.PIPS Mage PIP=[iter(lattr(me/PIP_*),[if(strmatch(%0,extract(xget(me,%i0),1,1,:)),[setq(F,[setunion(%qF,%i0)])])], ,@@)]%qF
&F.PROCESS.PIP Mage PIP=[setq(A,[xget(me,%0)])][setq(B,[xget(extract(%qA,1,1,:),_Mana)])][setq(C,extract(%qA,3,1,:))][setq(D,extract(%qA,4,1,:))][if(gte(sub(%qB,%qC),0),[set([extract(%qA,1,1,:)],_Mana:[sub(%qB,%qC)])][pemit(extract(%qA,1,1,:),[ansi(hr,MPIP:)]%b[ansi(hw,Took %qC Mana from you for Payment in Power for [extract(%qA,2,1,:)])])][set(me,%0:[extract(%qA,1,1,:)][chr(58)][extract(%qA,2,1,:)][chr(58)]%qC[chr(58)][sub(%qD,%qC)][chr(58)]0)],[set(me,%0:[extract(%qA,1,1,:)][chr(58)][extract(%qA,2,1,:)][chr(58)]%qC[chr(58)]%qD[chr(58)]1)][pemit(extract(%qA,1,1,:),[ansi(hr,MPIP:)]%b[ansi(hw,You could not pay your Payment in Power for [extract(%qA,2,1,:)])])])][if(not(gt(sub(%qD,%qC),0)),[set(me,%0:)])]
@Daily Mage PIP=[setq(A,[lattr(me/PIP*)])][if(gte(words(%qA),1),[iter(%qA,[u(F.PROCESS.PIP,%i0)], ,@@)])]
&TEST Mage PIP=[set(pmatch(monk)/_Template,1)]
&CMD.UPDATE Mage PIP=$+mpip/update *:@assert [isstaff(%#)];@assert [hasattr(me,PIP_%0)]={@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Can't find a Payment in Power record under ID %0)]};@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Updating Payment in Power Record %0 and taking Mana from player to make current.)];@trigger #8949/F.PROCESS.PIP=PIP_%0
&CMD.CLOSE Mage PIP=$+mpip/close *:@assert [isstaff(%#)];@assert [hasattr(me,PIP_%0)]={@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Can't find a Payment in Power record under ID %0)]};@set #8949=PIP_%0:;@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Removed Payment in Power Record under ID %0)]
&CMD.ADD Mage PIP=$+mpip/add *=*/*:@assert [isstaff(%#)];@assert [pmatch(%0)]={@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Can't find %0.)]};@assert [and(gte(%1,1),gte(before(%2,:),1))]={@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Daily amount and Total must be 1 or more.)]};@assert [gte(words(after(%2,:)),1)]={@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Must provide a reason.)]};@switch [attrcnt(me/PIP*)]=0,[set(me,PIP_0:[pmatch(%0)][chr(58)][after(%2,:)][chr(58)]%1[chr(58)][before(%2,:)][chr(58)]0)],[setq(A,[attrcnt(me/PIP*)])][set(me,PIP_%qA:[pmatch(%0)][chr(58)][after(%2,:)][chr(58)]%1[chr(58)][before(%2,:)][chr(58)]0)];@pemit %#=[ansi(hr,MPIP:)]%b[ansi(hw,Added a Payment in Power Record for %0 to pay for [after(%2,:)]. They will spend %1 Mana a day until they spend [before(%2,:)] total Mana.)];
&LIT.KEY #8949=[ljust([ansi(hw,ID)],5)][ljust([ansi(hw,NAME)],27)][rjust([ansi(hw,REASON)],27)][rjust([ansi(hw,DAILY)],10)][rjust([ansi(hw,REMAINING)],10)]%R%b[repeat(-,77)]